#include <Wire.h>
#include <LiquidCrystal_I2C.h>
#include <Adafruit_Fingerprint.h>
#include <Keypad.h>
#include <SoftwareSerial.h>

// Pin Configuration
#define RELAY_PIN 12
#define BUZZER_PIN 13
SoftwareSerial mySerial(2, 3); // TX, RX for fingerprint
Adafruit_Fingerprint finger(&mySerial);
LiquidCrystal_I2C lcd(0x27, 16, 2);

// Keypad Configuration
const byte ROWS = 4;
const byte COLS = 4;
char keys[ROWS][COLS] = {
  {'1','2','3','A'},
  {'4','5','6','B'},
  {'7','8','9','C'},
  {'*','0','#','D'}
};
byte rowPins[ROWS] = {4, 5, 6, 7};
byte colPins[COLS] = {8, 9, 10, 11};
Keypad keypad = Keypad(makeKeymap(keys), rowPins, colPins, ROWS, COLS);

const String correctPin = "2580";
int incorrectAttempts = 0;

void setup() {
  pinMode(RELAY_PIN, OUTPUT);
  pinMode(BUZZER_PIN, OUTPUT);
  digitalWrite(RELAY_PIN, LOW);
  lcd.init();
  lcd.backlight();
  Serial.begin(9600);
  finger.begin(57600);
  if (finger.verifyPassword()) {
    Serial.println("Fingerprint sensor found!");
  } else {
    Serial.println("Fingerprint sensor not found.");
    while (1);
  }
}

void loop() {
  lcd.clear();
  lcd.print("1: Biometric");
  lcd.setCursor(0, 1);
  lcd.print("2: PIN Code");
  
  while (true) {
    char key = keypad.getKey();
    if (key) {
      if (key == '1') {
        lcd.clear();
        lcd.print("Enter Biometric");
        int fingerID = getFingerprintID();
        
        if (fingerID > 0) {
          lcd.clear();
          lcd.print("Finger ID: ");
          lcd.print(fingerID);
          delay(2000);
          unlockDoor();  // Directly unlock if biometric is successful
        } else {
          lcd.clear();
          lcd.print("User Not Found");
          delay(3000);
        }
        break;
      }
      else if (key == '2') {
        lcd.clear();
        lcd.print("Enter PIN Code");
        enterPinCode();
        break;
      }
    }
  }
}

int getFingerprintID() {
  int result = finger.getImage();
  if (result != FINGERPRINT_OK) return -1;

  result = finger.image2Tz();
  if (result != FINGERPRINT_OK) return -1;

  result = finger.fingerFastSearch();
  if (result != FINGERPRINT_OK) return -1;

  return finger.fingerID;
}

void enterPinCode() {
  String enteredPin = "";
  lcd.clear();
  lcd.print("Enter Pin Code");

  while (true) {
    char key = keypad.getKey();
    if (key) {
      if (key == '*') {
        if (enteredPin == correctPin) {
          unlockDoor();
          return;
        } else {
          incorrectAttempts++;
          lcd.clear();
          lcd.print("Wrong Pin");
          delay(1000);
          if (incorrectAttempts >= 2l) {
            triggerAlarm();
            incorrectAttempts = 0;
          }
          return;
        }
      } else if (key == 'D') {
        if (enteredPin.length() > 0) {
          enteredPin.remove(enteredPin.length() - 1);
          lcd.clear();
          lcd.print("Enter Pin Code");
          lcd.setCursor(0, 1);
          lcd.print(enteredPin);
        }
      } else {
        if (enteredPin.length() < 4) {
          enteredPin += key;
          lcd.setCursor(0, 1);
          lcd.print(enteredPin);
        }
      }
    }
  }
}

void unlockDoor() {
  lcd.clear();
  lcd.print("Door Unlocked");
  digitalWrite(RELAY_PIN, HIGH);
  delay(10000);  // Keep door unlocked for 10 seconds
  digitalWrite(RELAY_PIN, LOW);
  lcd.clear();
  lcd.print("Door Locked");
  delay(2000);
}

void triggerAlarm() {
  lcd.clear();
  lcd.print("User Incorrect");
  digitalWrite(BUZZER_PIN, HIGH);
  delay(10000);
  digitalWrite(BUZZER_PIN, LOW);
  lcd.clear();
  lcd.print("System Reset");
  delay(2000);
}